import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version('7.1.0')
}

group = project.property("group")
version = project.property("version")
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.io/repository/nms/' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://maven.playpro.com' }
    maven { url = 'https://repo.bg-software.com/repository/api/' }
    maven { url = 'https://repo.bg-software.com/repository/public-libs/' }
    maven { url = 'https://repo.glaremasters.me/repository/towny/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.Drc-DEV:configlib:1.9.3'
    // Spigot jar
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    // CoreProtect
    compileOnly 'net.coreprotect:coreprotect:20.1', {
        exclude group: 'org.spigotmc'
        exclude group: 'org.bukkit'
    }
    // Plugin Hooks
    compileOnly 'com.wasteofplastic:AcidIsland:latest'
    compileOnly 'com.wasteofplastic:ASkyblock:latest'
    compileOnly 'world.bentobox:BentoBox:latest'
    compileOnly 'com.drtshock:FactionsUUID:latest'
    compileOnly 'com.drtshock:FactionsUUID-New:latest'
    compileOnly 'net.prosavage:FactionsX:latest'
    compileOnly 'me.ryanhamshire:GriefPrevention:latest'
    compileOnly 'com.massivecraft:Factions:latest'
    compileOnly 'com.massivecraft:MassiveCore:latest'
    compileOnly 'com.palmergames.bukkit.towny:towny:0.98.0.0'
    compileOnly 'com.stefthedev:Villages:latest'
    compileOnly 'com.plotsquared:PlotSquared-Core:6.6.2'
    compileOnly 'com.plotsquared:PlotSquared-Bukkit:6.6.2', { transitive = false }
}

processResources {
    filter ReplaceTokens, tokens: [
            "project.name"       : project.property("name"),
            "project.groupId"    : project.property("groupid"),
            "project.artifactId" : project.property("artifactid"),
            "project.version"    : project.property("version"),
            "project.description": project.property("description")
    ]
}

jar {
    archiveFileName.set(project.property("name").toString() + "-lib.jar")
}

shadowJar {
    relocate "pro.dracarys.configlib", project.property("groupid").toString() + "." + project.property("artifactid").toString() + ".shade.configlib"
    archiveFileName.set(project.property("name").toString() + "-${project.version}.jar")
    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

build {
    dependsOn shadowJar
    dependsOn publish
}

publish.shouldRunAfter shadowJar
shadowJar.shouldRunAfter build

publishing {
    repositories {
        mavenLocal()
    }
}